import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

group 'com.github.jonathanxd'
version '1.1.0'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'license'
apply plugin: 'com.github.johnrengelman.shadow'

license {
    header rootProject.file("LICENSE_HEADER")
}

defaultTasks 'licenseFormat', 'build', 'test', 'jar', 'shadowJar', 'apOnlyJar'

sourceCompatibility = 1.8


repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
}

configurations {
    apOnly
}

ext.bytecode_writer_version = '4.0.0.36-beta-bytecode'
ext.source_writer_version = '4.0.0.23-beta-source'
ext.kores_extra_version = '1.3.4'
ext.kores_version = '4.0.0.48-beta'
ext.jwiutils_version = '4.16.1'

dependencies {
    compile("com.github.JonathanxD.Kores-BytecodeWriter:Kores-BytecodeWriter:$bytecode_writer_version")
    compile("com.github.JonathanxD.Kores-SourceWriter:Kores-SourceWriter:$source_writer_version")
    compile("com.github.JonathanxD:Kores-Extra:$kores_extra_version")
    compile "com.github.JonathanxD.Kores:Kores:$kores_version"
    compile "com.github.JonathanxD:JwIUtils:$jwiutils_version"

    // Ap-only dependencies
    apOnly("com.github.JonathanxD.Kores-SourceWriter:Kores-SourceWriter:$source_writer_version")
    apOnly("com.github.JonathanxD:Kores-Extra:$kores_extra_version")
    apOnly "com.github.JonathanxD.Kores:Kores:$kores_version"
    apOnly "com.github.JonathanxD:JwIUtils:$jwiutils_version"

    testCompile "com.google.testing.compile:compile-testing:0.8"
    testCompile "junit:junit:4.12"
    testCompile files("${System.properties['java.home']}/../libs/tools.jar")
}

jar {
    from "$rootProject.rootDir/LICENSE"

    manifest {
        attributes 'Implementation-Title': baseName,
                'Implementation-Version': version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task apOnlyJar(type: ShadowJar, dependsOn: classes) {
    classifier = 'ap'
    from sourceSets.main.output
    configurations = [project.configurations.apOnly]
}

shadowJar { // Relocate
    relocate 'com.github.jonathanxd.kores', 'com.github.jonathanxd.buildergenerator.lib.kores'
    relocate 'com.github.jonathanxd.iutils', 'com.github.jonathanxd.buildergenerator.lib.iutils'
    relocate 'com.github.jonathanxd.bytecodedisassembler', 'com.github.jonathanxd.buildergenerator.lib.bytecodedisassembler'
    relocate 'org.json.simple', 'com.github.jonathanxd.buildergenerator.lib.json.simple'
    relocate 'org.objectweb.asm', 'com.github.jonathanxd.buildergenerator.lib.asm'
    relocate 'com.google', 'com.github.jonathanxd.buildergenerator.lib.com.google'
    relocate 'org.jetbrains', 'com.github.jonathanxd.buildergenerator.lib.org.jetbrains'
    relocate 'org.intellij', 'com.github.jonathanxd.buildergenerator.lib.org.intellij'
    relocate 'kotlin', 'com.github.jonathanxd.buildergenerator.lib.kotlin'

    dependencies {
        exclude(dependency("com.google.testing.compile:.*"))
        exclude(dependency("junit:junit:.*"))
        exclude(dependency("org.hamcrest:.*:.*"))
    }
}

apOnlyJar {
    relocate 'com.github.jonathanxd.kores', 'com.github.jonathanxd.buildergenerator.lib.kores'
    relocate 'com.github.jonathanxd.iutils', 'com.github.jonathanxd.buildergenerator.lib.iutils'
    relocate 'com.github.jonathanxd.bytecodedisassembler', 'com.github.jonathanxd.buildergenerator.lib.bytecodedisassembler'
    relocate 'org.json.simple', 'com.github.jonathanxd.buildergenerator.lib.json.simple'
    relocate 'org.objectweb.asm', 'com.github.jonathanxd.buildergenerator.lib.asm'
    relocate 'com.google', 'com.github.jonathanxd.buildergenerator.lib.com.google'
    relocate 'org.jetbrains', 'com.github.jonathanxd.buildergenerator.lib.org.jetbrains'
    relocate 'org.intellij', 'com.github.jonathanxd.buildergenerator.lib.org.intellij'
    relocate 'kotlin', 'com.github.jonathanxd.buildergenerator.lib.kotlin'

    dependencies {
        exclude(dependency("com.google.testing.compile:.*"))
        exclude(dependency("junit:junit:.*"))
        exclude(dependency("org.hamcrest:.*:.*"))
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
    archives apOnlyJar
}

license {
    ext.name = project.name
    ext.description = project.description
    ext.organization = project.organization
    ext.url = project.url
    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"

    header rootProject.file('LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
        kt = 'SLASHSTAR_STYLE'
    }
}

